find_package(BISON)
find_package(FLEX)

# The following should work, but doesn't take into account the additional
# headers and output files generated by bison for C++:
#  bison_target(PDDLParserSource parser.yy ${CMAKE_CURRENT_BINARY_DIR}/parser.bison.cc)
# We do it manually:
set(BISON_SOURCES ${CMAKE_CURRENT_SOURCE_DIR}/parser.yy)
set(BISON_GENERATED_SOURCES parser.bison.cc)
set(BISON_GENERATED_HEADERS parser.bison.hh location.hh position.hh stack.hh)
add_custom_command(
  OUTPUT ${BISON_GENERATED_SOURCES} ${BISON_GENERATED_HEADERS}
  DEPENDS ${BISON_SOURCES}
  COMMAND ${BISON_EXECUTABLE}
  ARGS -v -d -o parser.bison.cc ${BISON_SOURCES}
  COMMENT "[BISON] Generating parser source code"
  )
set_directory_properties(PROPERTIES ADDITIONAL_MAKE_CLEAN_FILES
  ${CMAKE_CURRENT_BINARY_DIR}/parser.bison.output)

flex_target(PDDLScanner scanner.ll ${CMAKE_CURRENT_BINARY_DIR}/scanner.flex.cc)

file(GLOB sources "${CMAKE_CURRENT_SOURCE_DIR}/*.cc")

set_source_files_properties(${BISON_GENERATED_SOURCES} ${BISON_GENERATED_HEADERS} GENERATED)

include_directories(${CMAKE_CURRENT_SOURCE_DIR})
include_directories(${CMAKE_CURRENT_BINARY_DIR})

add_library(
  parser
  STATIC
  ${sources}
  ${BISON_GENERATED_SOURCES}
  ${FLEX_PDDLScanner_OUTPUTS}
)

target_link_libraries(parser pddl)

add_executable(
  example
  EXCLUDE_FROM_ALL
  example.cc
)

target_link_libraries(example parser pddl)
